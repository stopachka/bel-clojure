sexp = string | list | abbrev_fn | comp | type_comp | quote | backquote | comma | splice | char | symbol | dot | number
list = lparen sexp? (<whitespace> sexp)* rparen
abbrev_fn = <'['> sexp? (<whitespace> sexp)* <']'>
comp = (symbol | no_comp) (<':'> symbol | no_comp)*
no_comp = <'~'> symbol
type_comp = symbol <'|'> symbol
<lparen> = <'('>
<rparen> = <')'>
string = <'"'> any_char* <'"'>
char = <'\\'> name
symbol = name
dot = #'\.'
quote = <'\''> sexp
backquote = <'`'> sexp
comma = <','> sexp
splice = <',@'> sexp
name = #'[a-zA-Z!\-*+_=>\/<\^][a-zA-Z0-9!\-*+_=>\/<\^\?]*'
number = #'[+-]?[0-9][0-9]*[.]?[0-9]*'
any_char = #'.'
whitespace = #'\s+'
